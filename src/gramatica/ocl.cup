package compil.inv.parser;

import java_cup.runtime.*;
import compil.inv.semantica.*;
import compil.inv.geracao.GeradorDeCodigo;

parser code {:

    GeradorDeCodigo ger = GeradorDeCodigo.getInstance();

    public void report_error(String message, Object info) {
   
        StringBuffer m = new StringBuffer("Error");
   
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            if (s.left >= 0) {
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        m.append(" : "+message);
        System.err.println(m);
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */

terminal CONTEXT, INV,
        OR, XOR, AND, NOT, LT, GT, LE, GE, EQUAL, NEQUAL,
        PLUS, MINUS, MULT, DIVIDE, 
        SEMICOL, COLON, DCOLON, LPAREN, RPAREN, COMMA, RARROW, DOT, BAR,
	IF, THEN, ELSE, ENDIF, IMPLIES, SIZE, INCLUDES, EXCLUDES, FORALL, SELECT, EXISTS,
        RCOL,LCOL,INCLUDESALL;

terminal String ID, STRING, NUMERO, TRUE, FALSE;

non terminal No ExpressoesOcl;
non terminal Restricao Restricao;
non terminal Classe DeclaracaoContexto, ClassifierContexto;
non terminal Expressao ExpOcl, ExpPrimaria;
non terminal Invariante InvContext;
non terminal Literal Literal;
non terminal Expressao ExpLogica, ExpRelacional;
non terminal Expressao ExpAditiva, ExpMultiplicativa, ExpUnaria;
non terminal Expressao ExpAditivaAux, ExpMultiAux, ExpPosFixada, ExpPosFixadaAux; 
non terminal Operador OperadorUnario, OperadorMais, OperadorMulti, OperadorRelacional, OperadorLogico;
non terminal No ChamadaPropriedade, ExpressaoIf, Caminho;
non terminal No ParametrosChamadaPropriedade, ParametrosChamadaPropriedadeOpt;
non terminal No ListaParametrosOpt, Declarador, ListaParametros, DeclaradorAux;
non terminal No EspecificadorTipoOpt, EspecificadorTipo, EspecificadorTipoSimples, CaminhoAux;
non terminal No OperadorColecao,Qualificadores,ChamadaPropriedadeColecao,QualificadoresOpt;

ExpressoesOcl ::= Restricao:r {: parser.ger.restricao(r); :} ExpressoesOcl {:System.out.println("casei");:}
                | ;

Restricao ::= DeclaracaoContexto:c InvContext:i {: parser.ger.invariante(i); RESULT = new Restricao(c); :};

DeclaracaoContexto ::= CONTEXT ClassifierContexto:c {: parser.ger.classe(c); RESULT = c;:} ; 

ClassifierContexto ::= ID:id {: RESULT = new Classe(id); :} 
                | ID:id1 COLON ID:id2 {: RESULT = new Classe(id1, id2); :} 
                | ID:id1 DCOLON ID:id2 {: RESULT = new Classe(id1, id2); :} ;

InvContext ::= INV COLON ExpOcl:e {: RESULT = new Invariante(e); :} InvContext:i {: parser.ger.invariante(i); :}  
                | 
                INV ID:id COLON ExpOcl:e {: RESULT = new Invariante(e, id); :} InvContext:i {: parser.ger.invariante(i); :} 
                | ;

ExpOcl ::= ExpLogica:e {: parser.ger.concatenarSubExpressoesDe(e); RESULT = new Expressao(e); :};

ExpLogica ::= ExpRelacional:r {: parser.ger.concatenarSubExpressoesDe(r); RESULT = new Expressao(r); :}
            | ExpRelacional:r1 {: parser.ger.concatenarSubExpressoesDe(r1); :} 
            OperadorLogico:o {: parser.ger.oprd(o); :} 
            ExpLogica:l1 {: parser.ger.concatenarSubExpressoesDe(l1); RESULT = new Expressao(o, r1, l1); :}
            ;

ExpRelacional ::= ExpAditiva:a {: parser.ger.concatenarSubExpressoesDe(a); RESULT = new Expressao(a); :} 
                | ExpAditiva:a1 {: parser.ger.concatenarSubExpressoesDe(a1); :}  
                OperadorRelacional:o {: parser.ger.oprd(o); :} 
                ExpAditiva:a2 {: parser.ger.concatenarSubExpressoesDe(a1); RESULT = new Expressao(o, a1, a2); :}
                ;


ExpAditiva ::= ExpMultiplicativa:emul {: parser.ger.concatenarSubExpressoesDe(emul); :} 
                ExpAditivaAux:eaux {: parser.ger.concatenarSubExpressoesDe(eaux); RESULT = new Expressao(emul, eaux); :};

ExpAditivaAux ::=  OperadorMais:o {: parser.ger.oprd(o); :} ExpMultiplicativa:op1 {: parser.ger.concatenarSubExpressoesDe(op1); :}
                ExpAditivaAux:op2 {: parser.ger.concatenarSubExpressoesDe(op2); RESULT = new Expressao(o, op1, op2);:}
		|  ;

ExpMultiplicativa ::= ExpUnaria:eun {: parser.ger.concatenarSubExpressoesDe(eun); :} ExpMultiAux:eaux {: parser.ger.concatenarSubExpressoesDe(eaux); RESULT = new Expressao(eun, eaux); :};


ExpMultiAux ::= OperadorMulti:o {: parser.ger.oprd(o); :} ExpUnaria:e1 {: parser.ger.concatenarSubExpressoesDe(e1); :} 
                ExpMultiAux:e2 {: parser.ger.concatenarSubExpressoesDe(e2); RESULT = new Expressao(o, e1, e2); :}
		|  ;

ExpUnaria ::= OperadorUnario:o {: parser.ger.oprd(o); :} ExpPosFixada:epf {: parser.ger.concatenarSubExpressoesDe(epf); RESULT = new Expressao(o, epf); :}
                | ExpPosFixada:epf {: parser.ger.concatenarSubExpressoesDe(epf); RESULT = new Expressao(epf); :};

ExpPosFixada ::= ExpPrimaria:ep ExpPosFixadaAux:epf {: RESULT = new Expressao(ep, epf); :};

/* TODO: gerar codigo para expPosFixadaAux e nao-terminais filhos */
ExpPosFixadaAux ::= DOT ChamadaPropriedade ExpPosFixadaAux
                    |
                    RARROW ChamadaPropriedadeColecao ExpPosFixadaAux
                    |
                    ; 

ExpPrimaria ::= Literal:l {: RESULT = new Expressao(l); :}
		| ChamadaPropriedade
		| LPAREN  ExpOcl RPAREN
		| ExpressaoIf;

ExpressaoIf ::= IF ExpOcl THEN ExpOcl ELSE ExpOcl ENDIF;


ChamadaPropriedadeColecao ::= OperadorColecao ParametrosChamadaPropriedadeOpt ;
							  

ChamadaPropriedade ::= Caminho ParametrosChamadaPropriedadeOpt                                                                    
                       ;

ParametrosChamadaPropriedadeOpt ::= ParametrosChamadaPropriedade | ; 

ParametrosChamadaPropriedade ::= LPAREN  Declarador ExpOcl RPAREN 	 
                                | LPAREN ExpOcl RPAREN
                                | LPAREN RPAREN ;
 
ListaParametros ::= ExpOcl ListaParametrosOpt ;
ListaParametrosOpt ::= COMMA ExpOcl ListaParametrosOpt | ; 

Declarador ::= ID DeclaradorAux EspecificadorTipoOpt BAR  ;

EspecificadorTipoOpt ::= SEMICOL ID COLON EspecificadorTipo EQUAL ExpOcl 
                         |
                         COLON ID
                         |
                         ;

DeclaradorAux ::= COMMA ID DeclaradorAux | ;

EspecificadorTipo ::= EspecificadorTipoSimples ;

EspecificadorTipoSimples ::= Caminho ;

Caminho ::= ID CaminhoAux ;

CaminhoAux ::= DCOLON ID CaminhoAux  | ;

Literal ::= STRING:s {: RESULT = new Literal(s, Literal.STRING); :} 
            | NUMERO:n {: RESULT = new Literal(n, Literal.NUMERO); :} 
            | TRUE:t {: RESULT = new Literal(t, Literal.BOOLEANO); :}
            | FALSE:f {: RESULT = new Literal(f, Literal.BOOLEANO); :} ;


OperadorColecao ::= INCLUDESALL | SIZE | INCLUDES | EXCLUDES | EXISTS | SELECT | FORALL ;
OperadorMais ::= PLUS {: RESULT = new Operador(Operador.MAIS);:} 
                | MINUS {:RESULT = new Operador(Operador.MENOS);:};
OperadorMulti ::= MULT {:RESULT = new Operador(Operador.VEZES);:}
                | DIVIDE {:RESULT = new Operador(Operador.DIVIDIDO);:} ;
OperadorUnario ::= MINUS {:RESULT = new Operador(Operador.MENOSU);:} 
                | NOT {:RESULT = new Operador(Operador.NOT);:};
OperadorLogico ::= AND {:RESULT = new Operador(Operador.AND);:}
                | OR {:RESULT = new Operador(Operador.OR);:}
                | XOR {:RESULT = new Operador(Operador.XOR);:} 
                | IMPLIES {: RESULT = new Operador(Operador.IMPLIES);:} ;
OperadorRelacional ::= EQUAL {:RESULT = new Operador(Operador.IGUAL);:}
                | GT {:RESULT = new Operador(Operador.MAIORQ);:}
                | LT {:RESULT = new Operador(Operador.MENORQ);:}
                | GE {:RESULT = new Operador(Operador.MAIORIGUAL);:}
                | LE {:RESULT = new Operador(Operador.MENORIGUAL);:}
                | NEQUAL {:RESULT = new Operador(Operador.DIF);:} ;